# $Id$
# Contributor: Hamlet <h@mlet.me>
# Contributor: Tom Vincent <http://tlvince.com/contact/>
# Original Maintainer: Eduardo Romero <eduardo@archlinux.org>
# Original Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=libgphoto2-nolimit
oldpkgname=libgphoto2
pkgver=2.4.14
pkgrel=1
pkgdesc="The core library of gphoto2. This variation is only to increase the limit of files !"
arch=(i686 x86_64)
url="http://www.gphoto.org"
license=(LGPL)
conflicts=('libgphoto2')
provides=(libgphoto2=${pkgver})

depends=('libexif' 'libjpeg>=8' 'gd' 'libltdl' 'libusb-compat' 'libtool>=2.4' 'libusb')
install=libgphoto2.install
options=('libtool')
source=(http://downloads.sourceforge.net/gphoto/${oldpkgname}-${pkgver}.tar.gz)
md5sums=('492bec63dd610906c3a28030be77e650')

build() {
  cd "${srcdir}/${oldpkgname}-${pkgver}"

  #Change the limit which cause "Fixed limit exceeded"
  sed -i "s#MAX_ENTRIES 1024#MAX_ENTRIES 8192#" libgphoto2/gphoto2-list.c

  ./configure \
    --prefix=/usr \
    --disable-rpath
  make
}

package() {
  cd "${srcdir}/${oldpkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Remove unused udev helper
  rm -rf "${pkgdir}/usr/lib/udev"

  install -m755 -d "${pkgdir}/usr/lib/udev/rules.d"
  LD_LIBRARY_PATH="${pkgdir}/usr/lib${LD_LIBRARY_PATH:+:}$LD_LIBRARY_PATH" \
  CAMLIBS="${pkgdir}/usr/lib/libgphoto2/${pkgver}" \
      "${pkgdir}/usr/lib/libgphoto2/print-camera-list" udev-rules version 175 > \
      "${pkgdir}/usr/lib/udev/rules.d/40-gphoto.rules"

  # Remove recursive symlink
  rm -f "${pkgdir}/usr/include/gphoto2/gphoto2"
}
